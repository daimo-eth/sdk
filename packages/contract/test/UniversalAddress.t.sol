// SPDX-License-Identifier: GPL-3.0-or-later
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import {IERC20} from "openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import {Create2} from "openzeppelin-contracts/contracts/utils/Create2.sol";

import {UniversalAddressManager} from "../src/UniversalAddressManager.sol";
import {ERC1967Proxy} from "openzeppelin-contracts/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import {UniversalAddressFactory} from "../src/UniversalAddressFactory.sol";
import {UniversalAddress, UniversalAddressRoute, UABridgeIntent} from "../src/UniversalAddress.sol";
import {SharedConfig} from "../src/SharedConfig.sol";
import {Call} from "../src/DaimoPayExecutor.sol";
import {DaimoPayExecutor} from "../src/DaimoPayExecutor.sol";
import {DaimoPayRelayer} from "../src/relayer/DaimoPayRelayer.sol";
import {TokenAmount, NativeTransfer} from "../src/TokenUtils.sol";

import {TestUSDC} from "./utils/DummyUSDC.sol";
import {TestDAI} from "./utils/DummyDAI.sol";
import {TestToken2Decimals} from "./utils/Dummy2DecimalsToken.sol";
import {DummyUniversalBridger} from "./utils/DummyUniversalBridger.sol";
import {BridgeReceiver} from "../src/UniversalAddressManager.sol";
import {UniversalAddressBridger} from "../src/UniversalAddressBridger.sol";
import {IDaimoPayBridger} from "../src/interfaces/IDaimoPayBridger.sol";
import {ReentrantToken} from "./utils/ReentrantToken.sol";

contract UniversalAddressTest is Test {
    // ---------------------------------------------------------------------
    // Test constants & actors
    // ---------------------------------------------------------------------
    address private constant ALICE =
        address(0xaAaAaAaaAaAaAaaAaAAAAAAAAaaaAaAaAaaAaaAa);
    address private constant ALEX =
        address(0xbBbBBBBbbBBBbbbBbbBbbbbBBbBbbbbBbBbbBBbB);
    address private constant RELAYER =
        address(0xCcCCccccCCCCcCCCCCCcCcCccCcCCCcCcccccccC);

    uint256 private constant SRC_CHAIN_ID = 1;
    uint256 private constant DST_CHAIN_ID = 10;

    uint256 private constant AMOUNT = 100e6; // 100 USDC (6-decimals)
    bytes32 private constant USER_SALT = bytes32(uint256(123));

    // ---------------------------------------------------------------------
    // Deployed contracts
    // ---------------------------------------------------------------------
    TestUSDC private usdc;
    UniversalAddressFactory private intentFactory;
    SharedConfig private cfg;
    DummyUniversalBridger private bridger;
    UniversalAddressManager private mgr;

    // ---------------------------------------------------------------------
    // Setup
    // ---------------------------------------------------------------------
    function setUp() public {
        // Token & balances
        usdc = new TestUSDC();
        usdc.transfer(ALICE, 500_000e6);
        usdc.transfer(RELAYER, 500_000e6);

        // Config
        SharedConfig implCfg = new SharedConfig();
        bytes memory cfgData = abi.encodeCall(
            SharedConfig.initialize,
            (address(this))
        );
        ERC1967Proxy cfgProxy = new ERC1967Proxy(address(implCfg), cfgData);
        cfg = SharedConfig(payable(address(cfgProxy)));
        cfg.setWhitelistedStable(address(usdc), true);

        // Core components
        intentFactory = new UniversalAddressFactory();
        bridger = new DummyUniversalBridger();

        // Deploy upgradeable UniversalAddressManager via ERC1967Proxy
        UniversalAddressManager impl = new UniversalAddressManager();
        bytes memory initData = abi.encodeCall(
            UniversalAddressManager.initialize,
            (intentFactory, bridger, cfg)
        );
        ERC1967Proxy proxy = new ERC1967Proxy(address(impl), initData);
        mgr = UniversalAddressManager(payable(address(proxy)));
    }

    // ---------------------------------------------------------------------
    // Helpers
    // ---------------------------------------------------------------------
    function _route()
        internal
        view
        returns (UniversalAddressRoute memory route)
    {
        route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdc,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
    }

    function _universalAddress(
        UniversalAddressRoute memory route
    ) internal view returns (address) {
        return intentFactory.getUniversalAddress(route);
    }

    function _receiverAddress(
        address universalAddress
    ) internal view returns (address payable) {
        UABridgeIntent memory intent = UABridgeIntent({
            universalAddress: universalAddress,
            relaySalt: USER_SALT,
            bridgeAmountOut: AMOUNT,
            bridgeToken: usdc,
            sourceChainId: SRC_CHAIN_ID
        });
        (address payable addr, ) = mgr.computeReceiverAddress(intent);
        return addr;
    }

    // Helper to create TokenAmount
    function _bridge(IERC20 tok) internal pure returns (TokenAmount memory) {
        return TokenAmount({token: tok, amount: AMOUNT});
    }

    // ---------------------------------------------------------------------
    // Source-chain startIntent
    // ---------------------------------------------------------------------
    function testStartIntent() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);
        address receiver = _receiverAddress(universalAddress);

        // Alice funds her UA vault.
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Start – no swap, same coin in/out.
        TokenAmount memory bridgeOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: AMOUNT
        });

        vm.prank(RELAYER);
        vm.expectEmit(true, true, true, true);
        emit IDaimoPayBridger.BridgeInitiated({
            fromAddress: address(mgr),
            fromToken: address(usdc),
            fromAmount: AMOUNT,
            toChainId: DST_CHAIN_ID,
            toAddress: receiver,
            toToken: address(usdc),
            toAmount: AMOUNT,
            refundAddress: universalAddress
        });
        mgr.startIntent(route, usdc, bridgeOut, USER_SALT, new Call[](0), "");

        assertTrue(mgr.receiverUsed(receiver));
    }

    // ---------------------------------------------------------------------
    // Destination-chain fastFinishIntent
    // ---------------------------------------------------------------------
    function testFastFinishIntent() public {
        testStartIntent(); // registers bridgedCoin mapping

        vm.chainId(DST_CHAIN_ID);
        UniversalAddressRoute memory route = _route();

        // Transfer bridged funds to the manager so it can forward them to the executor.
        vm.prank(RELAYER);
        usdc.transfer(address(mgr), AMOUNT);

        vm.prank(RELAYER);
        mgr.fastFinishIntent(
            route,
            new Call[](0),
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );

        assertEq(usdc.balanceOf(ALEX), AMOUNT);
    }

    // ---------------------------------------------------------------------
    // 2b. Destination-chain fastFinishIntent via DaimoPayRelayer
    function testFastFinishIntent_ViaRelayer() public {
        // 1) Source chain start
        testStartIntent();

        vm.chainId(DST_CHAIN_ID);
        UniversalAddressRoute memory route = _route();

        // Deploy a relayer contract with RELAYER as admin (and relayer role)
        DaimoPayRelayer relayerContract = new DaimoPayRelayer(RELAYER);

        // Fund the relayer contract with the tokenIn (USDC)
        vm.prank(RELAYER);
        usdc.transfer(address(relayerContract), AMOUNT);

        // Ensure relayer contract had balance
        assertEq(usdc.balanceOf(address(relayerContract)), AMOUNT);

        // Prepare parameters
        TokenAmount memory tokenIn = TokenAmount({
            token: IERC20(address(usdc)),
            amount: AMOUNT
        });
        TokenAmount memory bridgeTokenOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: AMOUNT
        });

        // RELAYER triggers fast-finish via the relayer contract
        vm.prank(RELAYER);
        relayerContract.uaFastFinish(
            new Call[](0), // preCalls
            mgr,
            route,
            tokenIn,
            bridgeTokenOut,
            USER_SALT,
            new Call[](0), // calls
            SRC_CHAIN_ID
        );

        // Beneficiary should have received the funds
        assertEq(usdc.balanceOf(ALEX), AMOUNT);
        assertEq(usdc.balanceOf(address(relayerContract)), 0);
    }

    // ---------------------------------------------------------------------
    // Claim flow without fast-finish
    // ---------------------------------------------------------------------
    function testClaimIntent_NoFastFinish() public {
        testStartIntent();

        vm.chainId(DST_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);
        address receiver = _receiverAddress(universalAddress);

        // Simulate that the bridge arrived
        vm.prank(ALICE);
        usdc.transfer(receiver, AMOUNT);
        assertEq(usdc.balanceOf(receiver), AMOUNT);

        // Test that the claimIntent can be permisionlessly called by ALICE if
        // there's no fast-finish.
        vm.prank(ALICE);
        assertEq(usdc.balanceOf(ALEX), 0);
        mgr.claimIntent(
            route,
            new Call[](0),
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );

        assertEq(usdc.balanceOf(ALEX), AMOUNT);
    }

    // ---------------------------------------------------------------------
    // Claim flow WITH fast-finish (relayer reimbursement)
    // ---------------------------------------------------------------------
    function testClaimIntent_WithFastFinish() public {
        // 1) Source chain start
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Alice funds her UA vault with the full amount
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        TokenAmount memory bridgeOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: AMOUNT
        });
        vm.prank(RELAYER);
        mgr.startIntent(route, usdc, bridgeOut, USER_SALT, new Call[](0), "");

        // 2) Destination chain – relayer performs fast-fill
        vm.chainId(DST_CHAIN_ID);

        uint256 relayerStartBal = usdc.balanceOf(RELAYER);

        // Relayer transfers the bridged funds to the manager before invoking fastFinishIntent
        assertEq(usdc.balanceOf(address(mgr)), 0);
        vm.prank(RELAYER);
        usdc.transfer(address(mgr), AMOUNT);

        vm.prank(RELAYER);
        mgr.fastFinishIntent(
            route,
            new Call[](0),
            usdc,
            bridgeOut,
            USER_SALT,
            SRC_CHAIN_ID
        );

        // Beneficiary should have already received the funds
        assertEq(usdc.balanceOf(ALEX), AMOUNT);
        // Relayer paid the amount upfront
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal - AMOUNT);
        // Check that receiverToRecipient updates to relayer as the new recipient
        address receiver = _receiverAddress(universalAddress);
        assertEq(mgr.receiverToRecipient(receiver), RELAYER);

        // 3) Simulate slow bridge landing at deterministic BridgeReceiver
        vm.prank(ALICE); // any account can transfer, acts as the bridge contract
        usdc.transfer(receiver, AMOUNT);

        // 4) Anyone can now call claimIntent – relayer must be reimbursed
        vm.prank(address(0xdead));
        mgr.claimIntent(
            route,
            new Call[](0),
            bridgeOut,
            USER_SALT,
            SRC_CHAIN_ID
        );

        // Relayer balance should be restored
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal);
        // Beneficiary balance remains unchanged (no double-pay)
        assertEq(usdc.balanceOf(ALEX), AMOUNT);
    }

    // ---------------------------------------------------------------------
    // startIntent with non-whitelisted token should revert
    // ---------------------------------------------------------------------
    function testStartIntent_NonWhitelistedToken_Reverts() public {
        // Deploy a second USDC-like token that is NOT whitelisted in cfg
        TestUSDC other = new TestUSDC();
        other.transfer(ALICE, 1_000e6);

        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: other,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice funds her UA vault with the un-whitelisted token
        vm.chainId(SRC_CHAIN_ID);
        vm.prank(ALICE);
        other.transfer(universalAddress, AMOUNT);

        // Expect revert because token is not whitelisted
        vm.expectRevert(bytes("UAM: whitelist"));
        mgr.startIntent(
            route,
            other,
            TokenAmount({token: IERC20(address(other)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // Duplicate fastFinishIntent should revert (receiver already used)
    // ---------------------------------------------------------------------
    function testFastFinishIntent_Duplicate_Reverts() public {
        testFastFinishIntent(); // performs the first fast-finish with RELAYER funding

        vm.chainId(DST_CHAIN_ID);
        UniversalAddressRoute memory route = _route();

        // Second attempt – should fail
        vm.expectRevert(bytes("UAM: already finished"));
        vm.prank(RELAYER);
        mgr.fastFinishIntent(
            route,
            new Call[](0),
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );
    }

    // ---------------------------------------------------------------------
    // fastFinishIntent on the wrong chain should revert
    // ---------------------------------------------------------------------
    function testFastFinishIntent_WrongChain_Reverts() public {
        testStartIntent();

        // We are still on the source chain (SRC_CHAIN_ID) instead of destination
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();

        vm.expectRevert(bytes("UAM: same chain finish"));
        vm.prank(RELAYER);
        mgr.fastFinishIntent(
            route,
            new Call[](0),
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );
    }

    // ---------------------------------------------------------------------
    // startIntent when balance exceeds PARTIAL_START_THRESHOLD should revert
    // if target amount is less than PARTIAL_START_THRESHOLD
    // ---------------------------------------------------------------------
    function testStartIntent_StartAmountLessThanThreshold_Reverts() public {
        // Set threshold to 50 USDC
        bytes32 thresholdKey = keccak256("PARTIAL_START_THRESHOLD");
        cfg.setNum(thresholdKey, 50e6);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Alice deposits 100 USDC into her UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, 100e6);

        // Relayer attempts to start with 49 USDC (< 50 USDC threshold)
        vm.expectRevert(bytes("UAM: amount < threshold"));
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: 49e6}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // startIntent when balance less than PARTIAL_START_THRESHOLD should use
    // the full balance of the UA vault
    // ---------------------------------------------------------------------
    function testStartIntent_BalanceLessThanThreshold() public {
        // Deploy second stablecoin with different number of decimals
        TestDAI dai = new TestDAI();
        cfg.setWhitelistedStable(address(dai), true);

        // Set PARTIAL_START_THRESHOLD to 50 USDC
        bytes32 thresholdKey = keccak256("PARTIAL_START_THRESHOLD");
        cfg.setNum(thresholdKey, 50e6);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // 25 DAI with some dust is sent to her UA vault
        dai.transfer(universalAddress, 25_000_000_000_000_111_111);

        // Prefund the executor with the required USDC to simulate a successful swap
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(address(exec), 25e6);

        // Relayer attempts to start with 25 USDC
        mgr.startIntent(
            route,
            dai,
            TokenAmount({token: IERC20(address(usdc)), amount: 25e6}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // startIntent when balance exceeds the PARTIAL_START_THRESHOLD should
    // let relayer use any partial amount greater than the threshold
    // ---------------------------------------------------------------------
    function testStartIntent_BalanceGreaterThanThreshold() public {
        // Deploy second stablecoin with different number of decimals
        TestDAI dai = new TestDAI();
        cfg.setWhitelistedStable(address(dai), true);

        // Set PARTIAL_START_THRESHOLD to 50 USDC
        bytes32 thresholdKey = keccak256("PARTIAL_START_THRESHOLD");
        cfg.setNum(thresholdKey, 50e6);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // 75 DAI is sent to her UA vault
        dai.transfer(universalAddress, 75e18);

        // Prefund the executor with the required USDC to simulate a successful
        // swap
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(address(exec), 60e6);

        // Relayer attempts to start with 60 USDC (greater than threshold but
        // less than balance)
        mgr.startIntent(
            route,
            dai,
            TokenAmount({token: IERC20(address(usdc)), amount: 60e6}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // startIntent with paymentToken != bridgeToken (swap path via executor)
    // ---------------------------------------------------------------------
    function testStartIntent_SwapPath() public {
        // Deploy second stable-coin (pretend USDT)
        TestUSDC usdt = new TestUSDC();
        cfg.setWhitelistedStable(address(usdt), true);

        vm.chainId(SRC_CHAIN_ID);

        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdt,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice funds her UA vault with USDC
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Prefund the executor with bridgeToken so balance check passes
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        usdt.transfer(address(exec), AMOUNT);

        // Alice kicks off startIntent (so she will also be refund recipient if needed)
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdt)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // Executor should have received USDC from the vault.
        // All USDT should have left the executor
        assertEq(usdc.balanceOf(address(exec)), AMOUNT);
        assertEq(usdt.balanceOf(address(exec)), 0);

        // Manager should not hold bridged tokens
        assertEq(usdt.balanceOf(address(mgr)), 0);

        // Verify that the receiver was marked as used
        UABridgeIntent memory intent = UABridgeIntent({
            universalAddress: universalAddress,
            relaySalt: USER_SALT,
            bridgeAmountOut: AMOUNT,
            bridgeToken: usdt,
            sourceChainId: SRC_CHAIN_ID
        });
        (address receiver, ) = mgr.computeReceiverAddress(intent);
        assertTrue(mgr.receiverUsed(receiver));
    }

    // ---------------------------------------------------------------------
    // Surplus refund path – manager held extra requiredToken before swap
    // ---------------------------------------------------------------------
    function testStartIntent_SurplusRefund() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Manager pre-holds surplus 50 USDC (e.g. previous dust)
        vm.prank(ALICE);
        usdc.transfer(address(mgr), 50e6);

        // Alice deposits full 100 USDC into her UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        uint256 aliceBalBefore = usdc.balanceOf(ALICE);

        // Alice initiates the intent so she is swapFunder
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // After refactor surplus may no longer be refunded immediately. Just ensure no underflow.
        assertGe(usdc.balanceOf(ALICE), aliceBalBefore);
    }

    // ---------------------------------------------------------------------
    // claimIntent swap path – executor already holds requiredToken
    // ---------------------------------------------------------------------
    function testClaimIntent_SwapEqual() public {
        // Deploy alternate stablecoin USDT and whitelist
        TestUSDC usdt = new TestUSDC();
        cfg.setWhitelistedStable(address(usdt), true);

        // 1) Source chain startIntent: pay in USDC, will bridge USDC, final token USDT
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdt,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice deposits USDC into her UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Start the intent
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // 2) Destination chain – place bridged USDC at deterministic receiver
        vm.chainId(DST_CHAIN_ID);
        address receiver = _receiverAddress(universalAddress);
        vm.prank(ALICE);
        usdc.transfer(receiver, AMOUNT);

        // Prefund the executor with the required USDT to simulate a successful swap
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        usdt.transfer(address(exec), AMOUNT);

        uint256 callerBalBefore = usdt.balanceOf(address(this));

        mgr.claimIntent(
            route,
            new Call[](0),
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );

        // Beneficiary got USDT
        assertEq(usdt.balanceOf(ALEX), AMOUNT);
        // Funds were pulled from the receiver
        assertEq(usdc.balanceOf(receiver), 0);
        // Executor was given USDC for the swap
        assertEq(usdc.balanceOf(address(exec)), AMOUNT);
        // Executor gave its USDT after the swap
        assertEq(usdt.balanceOf(address(exec)), 0);
        // Manager has balances
        assertEq(usdt.balanceOf(address(mgr)), 0);
        assertEq(usdc.balanceOf(address(mgr)), 0);
        // Caller balance unchanged
        assertEq(usdt.balanceOf(address(this)), callerBalBefore);
    }

    // ---------------------------------------------------------------------
    // claimIntent swap path – toToken has fewer decimals than bridgeToken
    // ---------------------------------------------------------------------
    function testClaimIntent_toTokenFewerDecimals() public {
        // Create a token with 2 decimals (fewer than USDC's 6)
        TestToken2Decimals token2 = new TestToken2Decimals();

        // 1) Source chain startIntent: pay in USDC, will bridge USDC, final token USDT
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: token2,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice deposits 1.234567 USDC into her UA vault
        TokenAmount memory bridgeAmount = TokenAmount({
            token: IERC20(address(usdc)),
            amount: 1234567
        });
        vm.prank(ALICE);
        usdc.transfer(universalAddress, bridgeAmount.amount);

        // Start the intent
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            usdc,
            bridgeAmount,
            USER_SALT,
            new Call[](0),
            ""
        );

        // 2) Destination chain – place bridged USDC at receiver simulating a slow bridge
        vm.chainId(DST_CHAIN_ID);
        UABridgeIntent memory intent = UABridgeIntent({
            universalAddress: universalAddress,
            relaySalt: USER_SALT,
            bridgeAmountOut: bridgeAmount.amount,
            bridgeToken: usdc,
            sourceChainId: SRC_CHAIN_ID
        });
        (address receiver, ) = mgr.computeReceiverAddress(intent);
        vm.prank(ALICE);
        usdc.transfer(receiver, bridgeAmount.amount);

        // Expected: ceiling(1,234,567 / 10,000) = ceiling(123.4567) = 124 token2
        uint256 expectedToAmount = 124;
        // Prefund the executor with the required token2 to simulate a successful swap
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        token2.transfer(address(exec), expectedToAmount);

        // No relayer came to fastFinish
        // Call claimIntent, using the bridged token to finish the intent
        mgr.claimIntent(
            route,
            new Call[](0),
            bridgeAmount,
            USER_SALT,
            SRC_CHAIN_ID
        );

        // Beneficiary got token2
        assertEq(token2.balanceOf(ALEX), expectedToAmount);
        // Funds were pulled from the receiver
        assertEq(usdc.balanceOf(receiver), 0);
        // Executor was given USDC for the swap
        assertEq(usdc.balanceOf(address(exec)), bridgeAmount.amount);
        // Manager has no balances
        assertEq(token2.balanceOf(address(mgr)), 0);
        assertEq(usdc.balanceOf(address(mgr)), 0);
    }

    // ---------------------------------------------------------------------
    // claimIntent swap path – insufficient output from executor reverts
    // ---------------------------------------------------------------------
    function testClaimIntent_SwapDeficitPull() public {
        // Alternate stablecoin (pretend USDT) and whitelist
        TestUSDC usdt = new TestUSDC();
        cfg.setWhitelistedStable(address(usdt), true);

        // Fund relayer with plenty of USDT
        usdt.transfer(RELAYER, 500_000e6);

        // 1) Source chain – Alice starts an intent to receive USDT on dst chain
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdt,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        vm.prank(RELAYER);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // 2) Destination chain – place bridged USDC at deterministic receiver
        vm.chainId(DST_CHAIN_ID);
        address receiver = _receiverAddress(universalAddress);
        vm.prank(ALICE);
        usdc.transfer(receiver, AMOUNT);

        // Prefund executor with only 60 USDT (< required 100) so manager will pull deficit
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        usdt.transfer(address(exec), 60e6);

        uint256 relayerBalBefore = usdt.balanceOf(RELAYER);

        // Expect insufficient-output revert due to deficit
        vm.prank(RELAYER);
        vm.expectRevert(bytes("DPCE: insufficient output"));
        mgr.claimIntent(
            route,
            new Call[](0),
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            SRC_CHAIN_ID
        );
        assertEq(usdt.balanceOf(RELAYER), relayerBalBefore);
    }

    // ---------------------------------------------------------------------
    // startIntent deficit pull – executor returns < requiredAmount
    // ---------------------------------------------------------------------
    function testStartIntent_DeficitPull() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Alice funds her UA vault with 100 USDC
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Craft swap call that moves 90 USDC from executor to dummy address,
        // leaving only 10 USDC so the manager must top-up 90 from Alice.
        Call[] memory swapCalls = new Call[](1);
        swapCalls[0] = Call({
            to: address(usdc),
            value: 0,
            data: abi.encodeWithSelector(
                IERC20.transfer.selector,
                address(0xdead),
                90e6
            )
        });

        vm.prank(ALICE);
        vm.expectRevert(bytes("DPCE: insufficient output"));
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            swapCalls,
            ""
        );

        // Revert is expected – no further assertions.
    }

    // ---------------------------------------------------------------------
    // startIntent insufficient-output revert – executor returns 0 of requiredToken
    // ---------------------------------------------------------------------
    function testStartIntent_InsufficientOutput_Reverts() public {
        // Deploy alternate stable-coin (pretend USDT) and whitelist it
        TestUSDC usdt = new TestUSDC();
        cfg.setWhitelistedStable(address(usdt), true);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdt,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice deposits USDC into her UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Expect the call to revert because executor will return zero USDT,
        // triggering "DPCE: insufficient output" inside DaimoPayExecutor.
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // claimIntent called before bridged funds arrive should revert (underflow)
    // ---------------------------------------------------------------------
    function testClaimIntent_Underflow_Reverts() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Alice funds her UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // Switch to destination chain but do NOT pre-place additional bridged funds.
        vm.chainId(DST_CHAIN_ID);

        // Pass bridgeAmountOut larger than what will be swept (AMOUNT+1) to force underflow.
        vm.expectRevert(bytes("UAM: insufficient bridge"));
        mgr.claimIntent(
            route,
            new Call[](0),
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT + 1}),
            USER_SALT,
            SRC_CHAIN_ID
        );
    }

    // ---------------------------------------------------------------------
    // startIntent invoked on destination chain should revert
    // ---------------------------------------------------------------------
    function testStartIntent_OnDestinationChain_Reverts() public {
        vm.chainId(DST_CHAIN_ID); // Same as intent.toChainId
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        vm.expectRevert(bytes("UAM: start on dest chain"));
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // refundIntent with whitelisted stable-coin should revert when destination
    // chain IS supported by the bridger
    // ---------------------------------------------------------------------
    function testRefundIntent_WhitelistedToken_Reverts() public {
        vm.chainId(SRC_CHAIN_ID); // Source chain – intentSent is required
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Mark the destination chain as SUPPORTED by the DummyUniversalBridger
        // so that refunds of whitelisted tokens are disallowed.
        // mapping(uint256 => address) is stored at slot 0.
        bytes32 mappingSlot = keccak256(
            abi.encode(uint256(DST_CHAIN_ID), uint256(0))
        );
        vm.store(
            address(bridger),
            mappingSlot,
            bytes32(uint256(uint160(address(usdc))))
        );

        // Set minimum start token out to 50 USDC
        bytes32 minKey = keccak256("MIN_START_TOKEN_OUT");
        cfg.setNum(minKey, 50e6);

        // Transfer 51 USDC to manager (above 50 USDC threshold)
        vm.prank(ALICE);
        usdc.transfer(universalAddress, 51e6);

        // This should fail because token is whitelisted **and** destination chain is supported
        vm.expectRevert(bytes("UAM: refund denied"));
        mgr.refundIntent(route, usdc);
    }

    // ---------------------------------------------------------------------
    // refundIntent success on source chain after startIntent (intentSent == true)
    // ---------------------------------------------------------------------
    function testRefundIntent_SourceChain_Succeeds() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Step 1: Alice funds UA vault with USDC and starts the intent
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );

        // Step 2: Deposit a non-whitelisted token into the vault AFTER startIntent
        TestUSDC stray = new TestUSDC();
        uint256 strayAmt = 42e6;
        stray.transfer(universalAddress, strayAmt);

        // Step 3: Call refundIntent to sweep the stray token back to refundAddress (ALICE)
        uint256 aliceBalBefore = stray.balanceOf(ALICE);
        mgr.refundIntent(route, stray);
        assertEq(stray.balanceOf(ALICE), aliceBalBefore + strayAmt);
    }

    // ---------------------------------------------------------------------
    // refundIntent succeeds with whitelisted token when destination chain is
    // NOT supported by the bridger
    // ---------------------------------------------------------------------
    function testRefundIntent_UnsupportedChain_Succeeds() public {
        vm.chainId(SRC_CHAIN_ID);

        // Use a destination chain that the bridger does NOT support (no stableOut set)
        uint256 unsupportedChainId = DST_CHAIN_ID + 1;
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: unsupportedChainId,
            toToken: usdc,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Fund the UA vault with some whitelisted token (USDC)
        uint256 refundAmount = 42e6;
        vm.prank(ALICE);
        usdc.transfer(universalAddress, refundAmount);

        uint256 aliceBalBefore = usdc.balanceOf(ALICE);
        mgr.refundIntent(route, usdc);

        // Alice should have received the refundAmount
        assertEq(usdc.balanceOf(ALICE), aliceBalBefore + refundAmount);
        // UA vault balance for USDC should now be zero
        assertEq(usdc.balanceOf(universalAddress), 0);
    }

    // ---------------------------------------------------------------------
    // Global pause switch – all mutating calls should revert
    // ---------------------------------------------------------------------
    function testGlobalPause_Reverts() public {
        // Pause via SharedConfig
        cfg.setPaused(true);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Fund UA vault
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Any state-changing call (e.g. startIntent) should now revert
        vm.expectRevert(bytes("UAM: paused"));
        mgr.startIntent(
            route,
            usdc,
            TokenAmount({token: IERC20(address(usdc)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // UniversalAddress cannot be initialised twice
    // ---------------------------------------------------------------------
    function testUniversalAddress_Reinitialise_Reverts() public {
        UniversalAddressRoute memory route = _route();
        // Deploy proxy via factory
        UniversalAddress vault = UniversalAddress(
            intentFactory.createUniversalAddress(route)
        );

        // Attempt to call initialize again – should revert (already initialised)
        bytes32 hash = keccak256(abi.encode(route));
        vm.expectRevert(abi.encodeWithSignature("InvalidInitialization()"));
        vault.initialize(hash);
    }

    // ---------------------------------------------------------------------
    // Reentrancy attack against startIntent should be blocked by ReentrancyGuard
    // ---------------------------------------------------------------------
    function testStartIntent_ReentrancyBlocked() public {
        // Deploy malicious re-entrant token that will attempt a nested call
        ReentrantToken evil = new ReentrantToken(payable(address(mgr)));
        cfg.setWhitelistedStable(address(evil), true);

        // Give Alice plenty of the token
        evil.transfer(ALICE, 1_000_000e6);

        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: evil,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice deposits funds into her UA vault
        vm.prank(ALICE);
        evil.transfer(universalAddress, AMOUNT);

        // Expect the nested call in token.transfer to trigger ReentrancyGuard revert
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            evil,
            TokenAmount({token: IERC20(address(evil)), amount: AMOUNT}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }

    // ---------------------------------------------------------------------
    // Finishing a same-chain intent with a non-whitelisted token should revert
    // ---------------------------------------------------------------------
    function testSameChainIntent_NonWhitelistedToken_Reverts() public {
        // Deploy a second USDC-like token that is NOT whitelisted in cfg
        TestUSDC other = new TestUSDC();
        other.transfer(ALICE, 1_000e6);

        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: other,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // Alice funds her UA vault with the un-whitelisted token
        vm.chainId(DST_CHAIN_ID);
        vm.prank(ALICE);
        other.transfer(universalAddress, AMOUNT);

        // Expect revert because token is not whitelisted
        vm.prank(RELAYER);
        vm.expectRevert(bytes("UAM: whitelist"));
        mgr.sameChainFinishIntent(route, other, AMOUNT, new Call[](0));
    }

    // ---------------------------------------------------------------------
    // Same-chain intent can be completed with no swap, using full UA balance
    // ---------------------------------------------------------------------
    function testSameChainIntent_NoSwap_FullBalance() public {
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // 1) Destination chain – Alice sends USDC to UA vault directly
        vm.chainId(DST_CHAIN_ID);
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        uint256 relayerStartBal = usdc.balanceOf(RELAYER);

        // 2) Relayer finalises via sameChainFinishIntent.
        vm.prank(RELAYER);
        mgr.sameChainFinishIntent(route, usdc, AMOUNT, new Call[](0));

        // 4) Assertions – beneficiary receives amount
        assertEq(usdc.balanceOf(ALEX), AMOUNT);
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal);
        // UA vault should now be empty on destination chain.
        assertEq(usdc.balanceOf(universalAddress), 0);
    }

    // ---------------------------------------------------------------------
    // Same-chain intent finish reverts if the vault balance is less than the
    // threshold and the relayer attempts to use a partial amount
    // ---------------------------------------------------------------------
    function testSameChainIntent_PartialBalanceLessThanThreshold_Reverts()
        public
    {
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Set PARTIAL_START_THRESHOLD to 50 USDC
        bytes32 thresholdKey = keccak256("PARTIAL_START_THRESHOLD");
        cfg.setNum(thresholdKey, 50e6);

        // 1) Destination chain – Alice sends 40 USDC to UA vault (<= threshold)
        // with some dust
        vm.chainId(DST_CHAIN_ID);
        vm.prank(ALICE);
        usdc.transfer(universalAddress, 40_000_001);

        // 2) Relayer tries to finish with partial amount
        vm.prank(RELAYER);
        vm.expectRevert(bytes("UAM: amount != balance"));
        mgr.sameChainFinishIntent(route, usdc, 40_000_000, new Call[](0));
    }

    // ---------------------------------------------------------------------
    // Same-chain intent finish reverts if output less than toAmount
    // ---------------------------------------------------------------------
    function testSameChainIntent_OutputLessThanToAmount_Reverts() public {
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // 1) Destination chain – Alice sends USDC to UA vault directly
        vm.chainId(DST_CHAIN_ID);
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT - 1);

        uint256 relayerStartBal = usdc.balanceOf(RELAYER);

        // 2) Relayer finalises via sameChainFinishIntent.
        vm.prank(RELAYER);
        vm.expectRevert();
        mgr.sameChainFinishIntent(route, usdc, AMOUNT, new Call[](0));

        // 4) Assertions – no changes to balances.
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal);
        assertEq(usdc.balanceOf(universalAddress), AMOUNT - 1);
    }

    // ---------------------------------------------------------------------
    // Same-chain intent finish reverts if output less than toAmount
    // ---------------------------------------------------------------------
    function testSameChainIntent_PaymentTokenFewerDecimals() public {
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Create a token with 2 decimals (fewer than USDC's 6)
        TestToken2Decimals token2 = new TestToken2Decimals();
        cfg.setWhitelistedStable(address(token2), true);

        // toAmount: 1.234567 USDC = 1,234,567 units (6 decimals)
        uint256 toAmount = 1234567;

        // 1) Destination chain – Alice sends USDC to UA vault directly
        vm.chainId(DST_CHAIN_ID);
        // Fund UA vault with 2 token2
        uint256 paymentAmount = 2e2;
        token2.transfer(universalAddress, paymentAmount);

        // 2) Relayer finalises via sameChainFinishIntent.
        // Prefund executor with toAmount USDC to simulate swap
        address executorAddr = address(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(executorAddr, toAmount);

        uint256 relayerStartBal = usdc.balanceOf(RELAYER);

        vm.prank(RELAYER);
        mgr.sameChainFinishIntent(route, token2, toAmount, new Call[](0));

        // 4) Assertions – beneficiary receives amount
        assertEq(usdc.balanceOf(ALEX), toAmount);
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal);
        // Check that the correct amount was transferred to executor
        // Expected: ceiling(1,234,567 / 10,000) = ceiling(123.4567) = 124 token2
        uint256 expectedAmount = 124; // ceiling division result (raw units)
        uint256 actualBalance = token2.balanceOf(executorAddr);
        assertEq(
            actualBalance,
            expectedAmount,
            "Should use ceiling division for fewer decimals"
        );
        // Check that the expected amount was pulled from the intent
        assertEq(
            token2.balanceOf(universalAddress),
            paymentAmount - expectedAmount
        );
    }

    // ---------------------------------------------------------------------
    // Test startIntent with payment token having fewer decimals than USDC
    // ---------------------------------------------------------------------
    function testStartIntent_PaymentTokenFewerDecimals() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Create a token with 2 decimals (fewer than USDC's 6)
        TestToken2Decimals token2 = new TestToken2Decimals();
        cfg.setWhitelistedStable(address(token2), true);

        // Fund UA vault with 2 token2
        uint256 vaultBalance = 2e2;
        token2.transfer(universalAddress, vaultBalance);

        // Bridge amount: 1.234567 USDC = 1,234,567 units (6 decimals)
        uint256 bridgeAmount = 1234567;
        TokenAmount memory bridgeOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: bridgeAmount
        });

        // Prefund executor with bridgeAmount USDC to simulate swap
        address executorAddr = address(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(executorAddr, bridgeAmount);

        vm.prank(RELAYER);
        mgr.startIntent(route, token2, bridgeOut, USER_SALT, new Call[](0), "");

        // Check that the correct amount was transferred to executor
        // Expected: ceiling(1,234,567 / 10,000) = ceiling(123.4567) = 124 token2
        uint256 expectedAmount = 124; // ceiling division result (raw units)
        uint256 actualBalance = token2.balanceOf(executorAddr);
        assertEq(
            actualBalance,
            expectedAmount,
            "Should use ceiling division for fewer decimals"
        );

        // Check that the expected amount was pulled from the intent
        assertEq(
            token2.balanceOf(universalAddress),
            vaultBalance - expectedAmount
        );

        // Verify receiver was used
        UABridgeIntent memory intent = UABridgeIntent({
            universalAddress: universalAddress,
            relaySalt: USER_SALT,
            bridgeAmountOut: bridgeAmount,
            bridgeToken: usdc,
            sourceChainId: SRC_CHAIN_ID
        });
        (address recvAddr, ) = mgr.computeReceiverAddress(intent);

        assertTrue(mgr.receiverUsed(recvAddr));
    }

    // ---------------------------------------------------------------------
    // Test startIntent with payment token having more decimals than USDC
    // ---------------------------------------------------------------------
    function testStartIntent_PaymentTokenMoreDecimals() public {
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Create a token with 18 decimals (more than USDC's 6)
        TestDAI dai = new TestDAI();
        cfg.setWhitelistedStable(address(dai), true);

        // Fund UA vault with 2 DAI
        uint256 vaultBalance = 2e18;
        dai.transfer(universalAddress, vaultBalance);

        // Bridge amount: 1.234567 USDC = 1,234,567 units (6 decimals)
        uint256 bridgeAmount = 1234567;
        TokenAmount memory bridgeOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: bridgeAmount
        });

        // Prefund executor with bridgeAmount USDC to simulate swap
        address executorAddr = address(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(executorAddr, bridgeAmount);

        vm.prank(RELAYER);
        mgr.startIntent(route, dai, bridgeOut, USER_SALT, new Call[](0), "");

        // Check that the correct amount was transferred to executor
        // Expected: 1,234,567 * 10^12 = 1,234,567,000,000,000,000 units
        uint256 expectedAmount = 1234567 * 10 ** 12; // multiply by 10^(18-6)
        uint256 actualBalance = dai.balanceOf(executorAddr);
        assertEq(
            actualBalance,
            expectedAmount,
            "Should multiply by 10^(18-6) for more decimals"
        );

        // Check that the expected amount was pulled from the intent
        assertEq(
            dai.balanceOf(universalAddress),
            vaultBalance - expectedAmount
        );

        // Verify receiver was used
        UABridgeIntent memory intent = UABridgeIntent({
            universalAddress: universalAddress,
            relaySalt: USER_SALT,
            bridgeAmountOut: bridgeAmount,
            bridgeToken: usdc,
            sourceChainId: SRC_CHAIN_ID
        });
        (address recvAddr, ) = mgr.computeReceiverAddress(intent);
        assertTrue(mgr.receiverUsed(recvAddr));
    }

    function testFinishVaultIntent() public {
        // 1) Source chain – start the intent.
        vm.chainId(SRC_CHAIN_ID);
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Alice funds her UA vault on source chain.
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        // Start the intent (no swap, same coin in/out).
        TokenAmount memory bridgeOut = TokenAmount({
            token: IERC20(address(usdc)),
            amount: AMOUNT
        });
        vm.prank(RELAYER);
        mgr.startIntent(route, usdc, bridgeOut, USER_SALT, new Call[](0), "");

        // 2) Destination chain – tokens end up in UA vault directly (instead of BridgeReceiver).
        vm.chainId(DST_CHAIN_ID);
        // Transfer bridged funds directly to the UA address on the destination chain.
        vm.prank(ALICE);
        usdc.transfer(universalAddress, AMOUNT);

        uint256 relayerStartBal = usdc.balanceOf(RELAYER);

        // 3) Relayer finalises via sameChainFinishIntent.
        vm.prank(RELAYER);
        mgr.sameChainFinishIntent(route, usdc, AMOUNT, new Call[](0));

        // 4) Assertions – beneficiary receives amount
        assertEq(usdc.balanceOf(ALEX), AMOUNT);
        assertEq(usdc.balanceOf(RELAYER), relayerStartBal);
        // UA vault should now be empty on destination chain.
        assertEq(usdc.balanceOf(universalAddress), 0);
    }

    // ---------------------------------------------------------------------
    // Test that UniversalAddress contracts emit NativeTransfer events on creation
    // and when receiving ETH
    // ---------------------------------------------------------------------
    function testUniversalAddressNativeTransferEvents() public {
        UniversalAddressRoute memory route = _route();
        address universalAddress = _universalAddress(route);

        // Pre-fund the universal address with 1 ETH before deployment
        vm.deal(universalAddress, 1 ether);

        // Deploy the UniversalAddress contract and expect NativeTransfer event for pre-existing balance
        vm.expectEmit(true, true, false, true);
        emit NativeTransfer(address(0), universalAddress, 1 ether);

        UniversalAddress deployedUA = UniversalAddress(
            intentFactory.createUniversalAddress(route)
        );
        assertEq(address(deployedUA), universalAddress);

        // Send 0.5 ETH to the deployed contract and expect NativeTransfer event
        vm.expectEmit(true, true, false, true);
        emit NativeTransfer(address(this), universalAddress, 0.5 ether);

        (bool success, ) = payable(universalAddress).call{value: 0.5 ether}("");
        require(success, "ETH transfer failed");

        // Verify the contract has the expected balance
        assertEq(universalAddress.balance, 1.5 ether);

        // Test sending ETH from a different address
        vm.deal(ALICE, 1 ether); // Give Alice some ETH first
        vm.prank(ALICE);
        vm.expectEmit(true, true, false, true);
        emit NativeTransfer(ALICE, universalAddress, 0.25 ether);

        (bool success2, ) = payable(universalAddress).call{value: 0.25 ether}(
            ""
        );
        require(success2, "ETH transfer from Alice failed");

        // Verify final balance
        assertEq(universalAddress.balance, 1.75 ether);
    }

    // ---------------------------------------------------------------------
    // Test startIntent with payment token having more decimals than USDC
    // ---------------------------------------------------------------------
    function testStartIntent_SwapPath_audit() public {
        // Add a whitelisted payment token with more than UniversalAssetManager.TOKEN_OUT_DECIMALS
        IERC20 dai = IERC20(new TestDAI());
        cfg.setWhitelistedStable(address(dai), true);

        vm.chainId(SRC_CHAIN_ID);
        bytes32 thresholdKey = keccak256("PARTIAL_START_THRESHOLD");
        cfg.setNum(thresholdKey, 50e6);

        UniversalAddressRoute memory route = UniversalAddressRoute({
            toChainId: DST_CHAIN_ID,
            toToken: usdc,
            toAddress: ALEX,
            refundAddress: ALICE,
            escrow: address(mgr)
        });
        address universalAddress = intentFactory.getUniversalAddress(route);

        // UA vault is funded with $10 of DAI (plus some dust for rounding)
        dai.transfer(universalAddress, 10_000_000_999_999_999_999);

        // Prefund the executor with bridgeToken so balance check passes with $10 of USDC
        DaimoPayExecutor exec = DaimoPayExecutor(mgr.executor());
        vm.prank(ALICE);
        usdc.transfer(address(exec), 10_000_000);

        // Alice kicks off startIntent
        vm.prank(ALICE);
        mgr.startIntent(
            route,
            dai,
            TokenAmount({token: IERC20(address(usdc)), amount: 10_000_000}),
            USER_SALT,
            new Call[](0),
            ""
        );
    }
}
